// project dependencies
ext {
VELOCITY_VER='1.7'
JACKSON_VER='2.8.6'
COMMONS_IO_VER='2.4'
STORY_LINE2_GLR_PARSER_VER='0.5-SNAPSHOT'
STORY_LINE2_CONFIG_VER='0.1'
STORY_LINE2_MORPH_VER='0.2'
DROPWIZARD_CORE_VER='1.0.5'
JERSEY_VER='2.25.+'
GRIZZLY_VER='2.3.28'
JSOUP_VER='1.9.1'
ELASTICSEARCH_CLIENT_TRANSPORT_VER='5.1.1'
MONGO_JAVA_DRIVER_VER='3.3.0'
DAGGER_VER='2.8'
}

project(':config') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	dependencies {
        compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VER}"
		compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${JACKSON_VER}"
    }
}

project(':server_web') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	apply plugin: "net.ltgt.apt"

	dependencies {
		compile "org.glassfish.jersey.containers:jersey-container-grizzly2-http:${JERSEY_VER}"
		// compile "org.glassfish.jersey.media:jersey-media-json-jackson:${JERSEY_VER}"
		compile "org.glassfish.grizzly:grizzly-http-server:${GRIZZLY_VER}"
		compile "ru.nlp_project.story_line2:config:${STORY_LINE2_CONFIG_VER}"
		compile ('org.apache.storm:storm-core:1.0.2') {
		// log4j-slf4j-impl-2.1
		exclude module: 'log4j-slf4j-impl'
		}
		// Dagger dependencies
		compile "com.google.dagger:dagger:${DAGGER_VER}"
		// Dagger dependencies
		apt "com.google.dagger:dagger-compiler:${DAGGER_VER}"
	}

	apply plugin: 'com.github.johnrengelman.shadow'

	// Include dependent libraries in archive.
	def mainClassName = "ru.nlp_project.story_line2.server_web.Application"

	shadowJar {
		mergeServiceFiles()
	  	manifest {
			attributes 'Description': 'This is an application JAR'
	    	attributes "Main-Class": "$mainClassName"
	 	}
	}

	// shadowJar.dependsOn('build')
	build.dependsOn('shadowJar')
}

project(':server_storm') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	apply plugin: "net.ltgt.apt"
	dependencies {
		compile ('org.apache.storm:storm-core:1.0.2') {
		// log4j-slf4j-impl-2.1
		exclude module: 'log4j-slf4j-impl'
		}
		compile 'de.undercouch:bson4jackson:2.7.0'
		compile "ru.nlp_project.story_line2:glr_parser:${STORY_LINE2_GLR_PARSER_VER}"
		compile "ru.nlp_project.story_line2:config:${STORY_LINE2_CONFIG_VER}"
		compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VER}"
		compile "org.mongodb:mongo-java-driver:${MONGO_JAVA_DRIVER_VER}"
		compile "org.elasticsearch.client:rest:${ELASTICSEARCH_CLIENT_TRANSPORT_VER}"
		// Dagger dependencies
		compile "com.google.dagger:dagger:${DAGGER_VER}"
		// Dagger dependencies
		apt "com.google.dagger:dagger-compiler:${DAGGER_VER}"
	}
}


project(':analyser') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	dependencies {
	    compile "org.jsoup:jsoup:${JSOUP_VER}"
        compile "ru.nlp_project.story_line2:glr_parser:${STORY_LINE2_GLR_PARSER_VER}"
        compile 'ru.nlp_project.story_line2:geo:0.1-SNAPSHOT'
    }
}

project(':geo') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)
}

project(':crawler') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	// before dependencies with annotation processing
	apply plugin: "net.ltgt.apt"
	dependencies {
		compile 'de.undercouch:bson4jackson:2.7.0'
		compile "org.mongodb:mongo-java-driver:${MONGO_JAVA_DRIVER_VER}"
		compile "commons-io:commons-io:${COMMONS_IO_VER}"
	    compile 'edu.uci.ics:crawler4j:4.2'
		compile "io.dropwizard:dropwizard-core:${DROPWIZARD_CORE_VER}"
		compile 'org.codehaus.groovy:groovy:2.4.7'
	    compile "org.jsoup:jsoup:${JSOUP_VER}"
		compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VER}"
		// Dagger dependencies
		compile "com.google.dagger:dagger:${DAGGER_VER}"
		// Dagger dependencies
		apt "com.google.dagger:dagger-compiler:${DAGGER_VER}"
	}
	apply plugin: 'com.github.johnrengelman.shadow'

	// Include dependent libraries in archive.
	def mainClassName = "ru.nlp_project.story_line2.crawler.CrawlerApplication"

	shadowJar {
		mergeServiceFiles()
	  	manifest {
			attributes 'Description': 'This is an application JAR'
	    	attributes "Main-Class": "$mainClassName"
	 	}
	}

	// shadowJar.dependsOn('build')
	build.dependsOn('shadowJar')
}


project(':glr_parser_debugger') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	dependencies {
        compile "org.apache.velocity:velocity:${VELOCITY_VER}"
        compile "ru.nlp_project.story_line2:glr_parser:${STORY_LINE2_GLR_PARSER_VER}"
    }
}

project(':glr_parser_testing') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	dependencies {
		compile "org.jsoup:jsoup:${JSOUP_VER}"
		compile "ru.nlp_project.story_line2:glr_parser:${STORY_LINE2_GLR_PARSER_VER}"
    }
}

project(':glr_parser') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	// before dependencies with annotation processing
	apply plugin: "net.ltgt.apt"
	dependencies {
	    compile 'com.beust:jcommander:1.47'
    	compile 'org.antlr:antlr4:4.3'
		compile 'org.apache.commons:commons-collections4:4.1'
		compile "com.fasterxml.jackson.core:jackson-databind:${JACKSON_VER}"
        compile 'ru.nlp_project.story_line2:token:0.1'
        compile "ru.nlp_project.story_line2:morph:${STORY_LINE2_MORPH_VER}"
		compile "ru.nlp_project.story_line2:config:${STORY_LINE2_CONFIG_VER}"
		// Dagger dependencies
		compile "com.google.dagger:dagger:${DAGGER_VER}"
		// Dagger dependencies
		apt "com.google.dagger:dagger-compiler:${DAGGER_VER}"
    }
}

project(':morph') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)


	dependencies {
//	    compile 'org.codehaus.woodstox:woodstox-core-lgpl:4.4.0'
		compile "commons-io:commons-io:${COMMONS_IO_VER}"
		compile 'net.sf.ehcache:ehcache-core:2.6.10'
		compile 'org.apache.commons:commons-lang3:3.3.2'
		compile "com.fasterxml.jackson.core:jackson-core:${JACKSON_VER}"
	}
}

project(':token') {
	javaProject(it)
	javaLogLibrary(it)
	javaTestLibrary(it)

	dependencies {
	    compile "commons-io:commons-io:${COMMONS_IO_VER}"
		compile 'org.apache.commons:commons-lang3:3.3.2'
		compile 'org.apache.opennlp:opennlp-tools:1.5.3'
	}
}

def javaProject(project) {
	project.ext.isJavaProject = true
	project.apply plugin: 'java'
	project.apply plugin: 'eclipse'

	project.repositories {
		maven {
			name "local-snapshots"
			url  "http://192.168.1.99:8082/nexus/content/repositories/snapshots/"
  		}
		maven {
			name "local-releases"
			url  "http://192.168.1.99:8082/nexus/content/repositories/releases/"
  		}
		mavenCentral()
		mavenLocal()
    }
}

def javaLogLibrary(project) {
    project.dependencies {
		/*
		Logback-classic module requires the presence of slf4j-api.jar and
		logback-core.jar in addition to logback-classic.jar on the classpath.
		*/
		compile 'org.slf4j:slf4j-api:1.7.22'
		compile 'ch.qos.logback:logback-classic:1.1.8'
    }
}

def javaTestLibrary(project) {
    project.dependencies {
		testCompile "commons-io:commons-io:${COMMONS_IO_VER}"
		testCompile "junit:junit:4.12"
		testCompile 'org.mockito:mockito-core:2.5.7'
		testCompile 'org.hamcrest:hamcrest-core:1.3'
    }

}
